import jsettlers.shaders.compile.ShaderCompilationTask

apply plugin: 'com.android.library'


def shaderDefinitionDir = 'src/main/glsl/'
def shaderJavaOutputDir = 'src/generated/java'
def shaderResOutputDir = 'src/generated/resources'

android {
    compileSdkVersion rootProject.ext.androidCompileSdkVersion
    buildToolsVersion rootProject.ext.androidBuildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.ext.androidMinSdkVersion
        targetSdkVersion rootProject.ext.androidTargetSdkVersion
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src/main/java']
            resources.srcDirs = ['src/main/resources', shaderResOutputDir]
            res.srcDirs = ['src/main/res']
            assets.srcDirs = ['assets']
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation project(':go.graphics')
}


clean {
    delete file(shaderJavaOutputDir)
    delete file(shaderResOutputDir)
}

task compileShaders(type: ShaderCompilationTask) {
    inputDirectory = file(shaderDefinitionDir).toPath()
    generatedSourcesDirectory = file(shaderJavaOutputDir).toPath()
    generatedResourcesDirectory = file(shaderResOutputDir).toPath()
}

tasks.whenTaskAdded{ task ->
    if(task.name == "generateDebugResources" || task.name == "processDebugJavaRes") {
        task.dependsOn(compileShaders)
    }
}
